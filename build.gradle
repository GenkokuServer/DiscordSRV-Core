/*
 * DiscordSRV2-Core: A library for generic Minecraft plugin development for all DiscordSRV2 projects
 * Copyright (C) 2018 DiscordSRV
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

plugins {
    id 'java'
    id 'idea'
    id 'checkstyle'
    id 'maven'
    id 'io.franzbecker.gradle-lombok' version '1.14'
}

group 'com.discordsrv'
version '2.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
}

lombok {
    version = '1.18.2'
    sha256 = ""
}

configurations {
    deployerJars
}

javadoc {
    options.memberLevel = JavadocMemberLevel.PUBLIC
    options.author()
    options.encoding = 'UTF-8'
    options.addBooleanOption('html5', true)
    options.addBooleanOption('Xdoclint:none', true)
}

checkstyle {
    configFile = rootProject.file('checkstyle.xml')
}
checkstyleMain {
    source = sourceSets.main.allJava
}
checkstyleTest {
    source = sourceSets.test.allJava
}

uploadArchives {
    repositories.mavenDeployer {
        configuration = project.configurations.deployerJars
        repository(url: System.env.mavenRepo) {
            authentication(userName: System.env.mavenRepoUsername, password: System.env.mavenRepoPassword)
        }
    }
    dependsOn check
}

test {
    reports {
        junitXml.enabled = false
        html.enabled = true
    }
    ignoreFailures = true
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.8.1'
    //noinspection UnnecessaryQualifiedReference
    distributionType = Wrapper.DistributionType.ALL
}

dependencies {
    deployerJars "org.apache.maven.wagon:wagon-http:+"
    implementation group: 'net.dv8tion', name: 'JDA', version: '3.6.0_376'
    implementation group: 'com.jagrosh', name: 'jda-utilities', version: '2.1.3'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    implementation group: 'com.google.guava', name: 'guava', version: '26.0-jre'
    implementation group: 'org.yaml', name: 'snakeyaml', version: '1.21'
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'
    testAnnotationProcessor project
}


